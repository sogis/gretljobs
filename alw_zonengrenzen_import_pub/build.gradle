import ch.so.agi.gretl.api.TransferSet
import ch.so.agi.gretl.tasks.*
import java.nio.file.Paths
import de.undercouch.gradle.tasks.download.Download

apply plugin: "de.undercouch.download"
apply plugin: "ch.so.agi.gretl"


defaultTasks "importData"


// Download-Verzeichnis
def pathToTempFolder = System.getProperty("java.io.tmpdir")
def pathToUnzipFolder = Paths.get(pathToTempFolder, "unzip_data")
def pathToDataZip = Paths.get(pathToTempFolder, "NachF_LWZ_INTERLIS2.zip")


task deleteFromZonengrenzenTables(type: SqlExecutor) {
    description = "LÃ¶scht die Daten aus dem Schema."
    database = [dbUriPub, dbUserPub, dbPwdPub]
    sqlFiles = ["delete_landwirtschaftliche_zonengrenzen_pub_tables.sql"]
}

task downloadKataloge(type: Download, dependsOn: 'deleteFromZonengrenzenTables'){
    description = "Download der Kataloge"
    src "https://models.geo.admin.ch/BLW/landwirtschaftliche_zonengrenzen_kataloge_20140701.xml"
    dest pathToTempFolder
    overwrite true
}

task importKataloge(type: Ili2pgReplace, dependsOn: 'downloadKataloge'){
     description = "Import der Kataloge"
     database = [dbUriPub, dbUserPub, dbPwdPub]
     dbschema = "alw_zonengrenzen_pub"
     models = "Landwirtschaftliche_Zonengrenzen_LV95_V1_2"
     dataFile = file(Paths.get(pathToTempFolder.toString(), "landwirtschaftliche_zonengrenzen_kataloge_20140701.xml"))
     dataset = "landwirtschaftliche_zonengrenzen_kataloge_20140701.xml-2938"
}

task "downloadData"(type: Download, dependsOn: 'importKataloge'){
    description = "Download Landwirtschaftliche Zonengrenzen"
    src "https://data.geo.admin.ch/ch.blw.landwirtschaftliche-zonengrenzen/NachF_LWZ_INTERLIS2.zip"
    dest pathToTempFolder
    overwrite true
}

task unzipData(type: Copy, dependsOn: 'downloadData'){
    description = "Unzip Data.zip"
    from zipTree(pathToDataZip)
    into file(pathToUnzipFolder)
    include "**/*.xtf"
}

task importData(type: Ili2pgReplace, dependsOn: 'unzipData'){
    description = "Import der Landwirtschaftlichen Zonengrenzen"
    database = [dbUriPub, dbUserPub, dbPwdPub]
    dbschema = "alw_zonengrenzen_pub"
    models = "Landwirtschaftliche_Zonengrenzen_LV95_V1_2"
    dataFile = file(Paths.get(pathToUnzipFolder.toString(), "NachF_LWZ_INTERLIS2.xtf"))
    dataset = "NachF_LWZ_INTERLIS2.xtf"
    strokeArcs = true
    disableValidation = true           //muss zwingend gesetzt sein wegen Heap Space
}
