import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.TransferSet

apply plugin: 'ch.so.agi.gretl'
apply plugin: 'org.hidetake.ssh'


remotes {
    sftpServer {
        host = "${sftpServerZivilschutz}"
        user = "${sftpUserZivilschutz}"
        password = "${sftpPwdZivilschutz}"
    }
}

def adressenDelete = 'amb_zivilschutz_adressen_staging_adressen_zivilschutz_delete.sql'
def adressenInsert = 'amb_zivilschutz_adressen_staging_adressen_zivilschutz.sql'
def csvFileName = "Adressen_Kanton_Solothurn_${new Date().format('yyyy-MM-dd')}.csv"

defaultTasks 'uploadCsvFile'

task stageAdressenZivilschutz(type: SqlExecutor){
    database = [dbUriPub, dbUserPub, dbPwdPub]
    sqlFiles = [adressenDelete, adressenInsert]
}

task exportAdressenZivilschutz(type: CsvExport, dependsOn: stageAdressenZivilschutz){
    database = [dbUriPub, dbUserPub, dbPwdPub]
    schemaName = "amb_zivilschutz_adressen_staging_pub"
    tableName = "adressen_zivilschutz"
    dataFile = csvFileName
    encoding = "UTF-8"
    valueSeparator = ';'
    valueDelimiter = null
    attributes = ['lokalisationsname', 'hausnummer', 'plz', 'ortschaft', 'gemeinde', 'gwr_egid', 'gwr_edid', 'koord_ost', 'koord_nord', 'astatus', 'objektname', 'grundstuecknummer', 'grundbuchkreis']
}

task replaceStatus(dependsOn: exportAdressenZivilschutz) {
    description = "Ersetzt astatus zu status."
    doLast {
        ant.replace(
        file: csvFileName,
        token: 'astatus',
        value: 'status',
        encoding: 'UTF-8')
    }
}

task deleteCsvFile(type: Delete){
    delete "${csvFileName}"
}

task uploadCsvFile(dependsOn: replaceStatus) {
    description = "Upload file to SFTP server; note that the upload is performed only in production environment"
    onlyIf {
        gretlEnvironment == 'production'
    }
    doLast {
        ssh.settings {
            knownHosts = allowAnyHosts
        }
        ssh.run {
            session(remotes.sftpServer) {
                put from: "${csvFileName}", into: "."
            }
        }
        println "File uploaded to SFTP server"
    }
    finalizedBy deleteCsvFile
}
