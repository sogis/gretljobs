description = """\n
Initialisierung des MJPNL Schemas mit diversen relativ statischen Tabellen (ähnlich Katalogen / Wertelisten)
und Geometrien die kaum ändern. Ausserdem Import der GELAN-Daten TOPIC Betriebsdaten_Strukturdaten

Fragen an: Andreas Neumann (AGI) und Odile Bruggisser (ARP)
"""

import ch.so.agi.gretl.tasks.*
import java.nio.file.Files
import java.nio.file.Paths
import java.io.File

apply plugin: 'ch.so.agi.gretl'
apply plugin: 'org.hidetake.ssh'

defaultTasks 'importGELAN'

def pathToTempFolder = System.getProperty("java.io.tmpdir")

remotes {
    sftpServer {
        host = "${sftpServerGelan}"
        user = "${sftpUserGelan}"
        password = "${sftpPwdGelan}"
    }
}

task copyXtfFile(type: Copy) {
    from "upload/"
    into "upload/"
    rename("uploadFile", "mjpnl_init.xtf")
}

task importMJPNL(type: Ili2pgReplace, dependsOn: 'copyXtfFile') {
  description = "Liest die Faunaliste, Floraliste und UZL-Subregionen ein."
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    dbschema = "arp_mjpnl_v1"
    models = "SO_ARP_MJPNL_20201026"
    topics = "SO_ARP_MJPNL_20201026.MJPNL"
    dataFile = "upload/mjpnl_init.xtf"
    dataset = "MJPNL Basisdaten"
    logFile = "ili2pgimport.log"
}

task downloadData(dependsOn: 'importMJPNL') {
    description = "Download file from SFTP server"
    doLast {
        ssh.settings {
            knownHosts = allowAnyHosts
        }
        ssh.run {
            session(remotes.sftpServer) {
                get from: "Prod/Export_ais_agrardaten.xtf", into: pathToTempFolder
            }
        }
        println "File downloaded from SFTP server"
    }
}

task importGELAN(type: Ili2pgReplace, dependsOn: downloadData) {
    description = 'Import resp. ersetzt täglich die GELAN-Daten (nur Topic: Betriebsdaten_Strukturdaten) in der Erfassungsdatenbank, Schema arp_mjpnl_v1'
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    dbschema = "arp_mjpnl_v1"
    models = 'SO_ALW_Landwirtschaft_Tierhaltung_20210426'
    topics = 'SO_ALW_Landwirtschaft_Tierhaltung_20210426.Betriebsdaten_Strukturdaten'
    dataFile = Paths.get(pathToTempFolder, 'Export_ais_agrardaten.xtf')
    dataset = 'GELAN'
}
