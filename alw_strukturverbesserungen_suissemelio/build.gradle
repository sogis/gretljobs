import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.TransferSet

apply plugin: 'ch.so.agi.gretl'
apply plugin: 'org.hidetake.ssh'


defaultTasks 'uploadXtfFile'

remotes {
    ftpServer {
        host = "${ftpServerInfogrips}"
        user = "${ftpUserEmapis}"
        password = "${ftpPwdEmapis}"
        port = 22
    }
}

def SVGIS_SuissemelioXtfFileName = 'emapis_so.xtf'

task transfer_SVGIS_Suissemelio(type: Db2Db){
    sourceDb = [dbUriEdit, dbUserEdit, dbPwdEdit]
    targetDb = [dbUriEdit, dbUserEdit, dbPwdEdit]
    transferSets = [
            new TransferSet('projekt.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_projektschwerpunkt', true),
            new TransferSet('beizugsgebiet.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_beizugsgebiet', true),
            new TransferSet('bewaesserung_flaeche.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_bew_flaechen_bewaesserung', true),
            new TransferSet('bewaesserung_linie.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_bewaesserung_linie', true),
            new TransferSet('bewaesserung_punkt.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_bewaesserung_punkt', true),
            new TransferSet('entwaesserung_bodenstruktur_flaeche.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_entw_bodenstruktur_flaeche', true),
            new TransferSet('entwaesserung_bodenstruktur_linie.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_entw_bodenstruktur_linie', true),
            new TransferSet('entwaesserung_bodenstruktur_pumpwerk.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_entw_bodenstruktur_pumpwerk', true),
            new TransferSet('elektroversorgung_linie.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_ev_linie', true),
            new TransferSet('elektroversorgung_punkt.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_ev_punkt', true),
            new TransferSet('oekologie_flaeche.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_oekologie_flaeche', true),
            new TransferSet('oekologie_linie.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_oekologie_linie', true),
            new TransferSet('oekologie_trockenmauer.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_oekologie_trockenmauer', true),
            new TransferSet('wasserversorgung_leitung.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_wv_leitung_wasserversorgung', true),
            new TransferSet('wasserversorgung_punkt.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_wasserversorgung_punkt', true),
            new TransferSet('wegebau_linie.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_wegebau_linie', true),
            new TransferSet('wege_bruecke_lehnenviadukt.sql', 'alw_strukturverbesserungen_suissemelio.raeumlicheelemnte_wege_bruecke_lehnenviadukt', true)
    ];
}

task exportSVGIS_Suissemelio(type: Ili2pgExport, dependsOn: 'transfer_SVGIS_Suissemelio') {
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    models = 'Strukturverbesserungen_LV95_V2'
    dbschema = 'alw_strukturverbesserungen_suissemelio'
    dataFile = file(SVGIS_SuissemelioXtfFileName)
    disableValidation = true
}

task validateSVGIS_Suissemelio(type: IliValidator, dependsOn: 'exportSVGIS_Suissemelio') {
    dataFiles = [file(SVGIS_SuissemelioXtfFileName)]
    logFile = "ilivalidator_SVGIS_Suissemelio.log"
    allObjectsAccessible = true
    failOnError = true
}

task deleteXtfFile(type: Delete){
    delete "${SVGIS_SuissemelioXtfFileName}"
}

task uploadXtfFile (dependsOn: validateSVGIS_Suissemelio){
    description = "Upload XtfFile to FTP server"
    doLast {
        ssh.settings {
            knownHosts = allowAnyHosts
        }
        ssh.run {
            session(remotes.ftpServer) {
                put from: "${SVGIS_SuissemelioXtfFileName}", into: "."
            }
        }
        println "File uploaded to FTP server"
    }
    finalizedBy deleteXtfFile
}

