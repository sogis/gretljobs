/*
Import GELAN-Daten in die Edit-DB
*/
import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.TransferSet
import java.nio.file.Paths


apply plugin: 'ch.so.agi.gretl'
apply plugin: 'org.hidetake.ssh'

defaultTasks "refreshSolr"

def pathToTempFolder = System.getProperty("java.io.tmpdir")

remotes {
    sftpServer {
        host = "${sftpServerGelan}"
        user = "${sftpUserGelan}"
        password = "${sftpPwdGelan}"
    }
}

task downloadData () {
    description = "Download file from SFTP server"
    doLast {
        ssh.settings {
            knownHosts = allowAnyHosts
        }
        ssh.run {
            session(remotes.sftpServer) {
                get from: "Prod/Export_ais_agrardaten.xtf", into: pathToTempFolder
            }
        }
        println "File downloaded from SFTP server"
    }
}

task importGELAN(type: Ili2pgReplace, dependsOn: downloadData) {
    description = 'Import resp. ersetzt t√§glich die GELAN-Daten (Topic: BFF_Qualitaet, Betriebsdaten_Strukturdaten, Datentransfer) in der Erfassungsdatenbank'
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    dbschema = "alw_landwirtschaft_tierhaltung_v1"
    models = 'SO_ALW_Landwirtschaft_Tierhaltung_20210426'
    dataFile = Paths.get(pathToTempFolder, 'Export_ais_agrardaten.xtf')
    dataset = 'agrardaten'
}

task transferALWAgrardaten(type: Db2Db, dependsOn: importGELAN){
    description = "Datenumbau in das Publikationsmodell"
    sourceDb = [dbUriEdit, dbUserEdit, dbPwdEdit]
    targetDb = [dbUriPub, dbUserPub, dbPwdPub]
    sqlParameters = [publikationsjahr_standort: '2022', publikationsjahr_flaechenerhebung: '2021']
    transferSets = [
        new TransferSet("dataset.sql",'alw_landwirtschaft_tierhaltung_pub_v1.t_ili2db_dataset',true),
        new TransferSet("basket.sql",'alw_landwirtschaft_tierhaltung_pub_v1.t_ili2db_basket',true),
        new TransferSet("bff_qualitaet.sql",'alw_landwirtschaft_tierhaltung_pub_v1.bff_qualitaet_bff_qualitaet',true),
        new TransferSet("bewirtschaftungseinheit.sql",'alw_landwirtschaft_tierhaltung_pub_v1.betrbsdttrktrdten_bewirtschaftungseinheit',true),
        new TransferSet("kultur_flaeche.sql",'alw_landwirtschaft_tierhaltung_pub_v1.betrbsdttrktrdten_kultur_flaeche',true),
        new TransferSet("kultur_punktelement.sql",'alw_landwirtschaft_tierhaltung_pub_v1.betrbsdttrktrdten_kultur_punktelement',true),
        new TransferSet("tierstandorte.sql",'alw_landwirtschaft_tierhaltung_pub_v1.betrbsdttrktrdten_tierstandort',true)
    ];
}

task refreshSolr(type:Exec, dependsOn:'transferALWAgrardaten') {
	commandLine 'curl', '-i', '--max-time', '5', solrIndexupdaterBaseUrl + '/queue?ds=ch.so.alw.bienenstandorte_und_sperrgebiete.bienenstandorte,ch.so.alw.landwirtschaft_tierhaltung.kulturflaechen_bff_qualitaetsstufe_I,ch.so.alw.landwirtschaft_tierhaltung.kulturflaechen_bff_qualitaetsstufe_I_geschuetzt,ch.so.alw.landwirtschaft_tierhaltung.kulturflaechen_hauptkategorien,ch.so.alw.landwirtschaft_tierhaltung.kulturflaechen_hauptkategorien_geschuetzt,ch.so.alw.landwirtschaft_tierhaltung.kultur_punktelement_bff_qualitaetsstufe_I,ch.so.alw.landwirtschaft_tierhaltung.kultur_punktelement_bff_qualitaetsstufe_I_geschuetzt'
}
