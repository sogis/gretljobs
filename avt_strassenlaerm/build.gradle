apply plugin: 'ch.so.agi.gretl'
apply plugin: 'de.undercouch.download'

import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.TransferSet
import java.nio.file.Paths

defaultTasks 'publishPub'
//defaultTasks 'replaceDataset'

ext {
    pathToTempFolder = System.getProperty("java.io.tmpdir")
    xtfFilePath = Paths.get(pathToTempFolder.toString(), 'strassenlaerm_KGDM_SO_20200115.xtf')
    filename = 'strassenlaerm_KGDM_SO_20200115.xtf'
}

task downloadData(type: Download) {
    //src 'https://s3.eu-central-1.amazonaws.com/ch.so.avt.strassenlaerm/strassenlaerm_KGDM_20191023.xtf'
    src 'https://s3.eu-central-1.amazonaws.com/ch.so.avt.strassenlaerm/' + filename
    dest pathToTempFolder
    overwrite true

    doLast {
        println "File downloaded to: " + pathToTempFolder
    }    
}


task replaceDataset (type: Ili2pgReplace, dependsOn: downloadData) {
    description = "Ersetzt die Daten in der Edit-DB."
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    dbschema = "avt_strassenlaerm_v1"
    models = "SO_AVT_Strassenlaerm_20190806"
    disableValidation = true
    importTid = true
    dataset = "data"
    dataFile = file(xtfFilePath)
    //dataFile = "strassenlaerm.xtf" // name is injected by jenkins
}

task publishEdit(type: Publisher, dependsOn: replaceDataset){
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    dataIdent = "ch.so.avt.strassenlaerm.relational"
    dbSchema = "avt_strassenlaerm_v1"
    dataset = "data"
    target = ["sftp://$sftpServerSogis/$sftpUserSogis", sftpUserSogis, sftpPwdSogis]
    kgdiService = [simiMetadataServiceUrl, simiMetadataServiceUser, simiMetadataServicePwd]
    kgdiTokenService = [simiTokenServiceUrl, simiTokenServiceUser, simiTokenServicePwd]
    grooming = new File(file(projectDir).getParentFile(), "publisher_grooming.json")
    if (findProperty('ilivalidatorModeldir')) modeldir = ilivalidatorModeldir
}

task transferData(type: Db2Db, dependsOn: publishEdit) {
    sourceDb = [dbUriEdit, dbUserEdit, dbPwdEdit]
    targetDb = [dbUriPub, dbUserPub, dbPwdPub]
    transferSets = [
            new TransferSet("transform_immission_strasse_pointofdetermination.sql", 'avt_strassenlaerm_pub_v1.strassenlaerm_belastung', true)
    ];
}

task publishPub(type: Publisher, dependsOn:transferData){
    database = [dbUriPub, dbUserPub, dbPwdPub]
    dataIdent = "ch.so.avt.strassenlaerm"
    dbSchema = "avt_strassenlaerm_pub_v1"
    modelsToPublish = "SO_AVT_Strassenlaerm_Publikation_20190802"
    userFormats = true
    target = ["sftp://$sftpServerSogis/$sftpUserSogis", sftpUserSogis, sftpPwdSogis]
    kgdiService = [simiMetadataServiceUrl, simiMetadataServiceUser, simiMetadataServicePwd]
    kgdiTokenService = [simiTokenServiceUrl, simiTokenServiceUser, simiTokenServicePwd]
    grooming = new File(file(projectDir).getParentFile(), "publisher_grooming.json")
    if (findProperty('ilivalidatorModeldir')) modeldir = ilivalidatorModeldir
}
