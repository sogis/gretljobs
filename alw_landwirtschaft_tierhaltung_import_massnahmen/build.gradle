/*
Import GELAN-Daten Bodenbedeckung in die Edit-DB
*/
import ch.so.agi.gretl.tasks.*
import java.nio.file.Paths


apply plugin: 'ch.so.agi.gretl'
apply plugin: 'org.hidetake.ssh'

defaultTasks "importGELAN"

def pathToTempFolder = System.getProperty("java.io.tmpdir")

remotes {
    sftpServer {
        host = "${sftpServerGelan}"
        user = "${sftpUserGelan}"
        password = "${sftpPwdGelan}"
        // Skip host key checking outside the following environments:
        if ( !(gretlEnvironment in ['test', 'integration', 'production']) ) { // uses the Groovy membership operator
            knownHosts = allowAnyHosts
        }
    }
}

task downloadData () {
    description = "Download file from SFTP server"
    doLast {
        ssh.run {
            session(remotes.sftpServer) {
                get from: "Prod/Export_ais_pp-mg.xtf", into: pathToTempFolder
            }
        }
        println "File downloaded from SFTP server"
    }
}

task checkDownloadedFileSize(dependsOn: downloadData) {
    description = "Check size of downloaded GELAN data file"
    doLast {
        def downloadedFile = file(Paths.get(pathToTempFolder, "Export_ais_pp-mg.xtf").toFile())
        if (!downloadedFile.exists()) {
            throw new GradleException("Erwartete Datei wurde nicht gefunden: ${downloadedFile}")
        }

        long minSizeBytes = 50L * 1024L * 1024L  // 50 MB
        if (downloadedFile.length() < minSizeBytes) {
            throw new GradleException(
                "Datei ist zu klein (${downloadedFile.length()} Bytes). " +
                "Erwartet mindestens ${minSizeBytes} Bytes (50 MB)."
            )
        }

        println "DateigrÃ¶sse OK: ${downloadedFile.length()} Bytes"
    }
}

task importGELAN(type: Ili2pgReplace, dependsOn: checkDownloadedFileSize) {
    description = 'Import resp. ersetzt einmal im Monat die GELAN-Daten (Topic: Massnahmen_Perimeter, Datentransfer) in der Erfassungsdatenbank'
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    dbschema = "alw_landwirtschaft_tierhaltung_v1"
    models = 'SO_ALW_Landwirtschaft_Tierhaltung_20210426'
    dataFile = Paths.get(pathToTempFolder, 'Export_ais_pp-mg.xtf').toFile()
    dataset = 'massnahmen'
}
