import ch.so.agi.gretl.api.TransferSet
import ch.so.agi.gretl.tasks.*
import java.nio.file.Paths

defaultTasks 'uploadMgdmNetzgebiete'

def pathToTempFolder = System.getProperty("java.io.tmpdir")

def mgdmNetzgebieteXtfFileName = "ch.so.awa.stromversorgungssicherheit_netzgebiete.mgdm.xtf"
def mgdmNetzgebieteZipFileName = "ch.so.awa.stromversorgungssicherheit_netzgebiete.mgdm.zip"


def aiLogin = aiUser + ":" + aiPwd

task transferNetzgebiete(type: Db2Db){
    sourceDb = [dbUriEdit, dbUserEdit, dbPwdEdit]
    targetDb = [dbUriPub, dbUserPub, dbPwdPub]
    transferSets = [
            new TransferSet('awa_stromversorgungssicherheit_pub_ebene_3.sql', 'awa_stromversorgungssicherheit_pub.netzbetreiber_ebene_3', true),
            new TransferSet('awa_stromversorgungssicherheit_pub_ebene_5.sql', 'awa_stromversorgungssicherheit_pub.netzbetreiber_ebene_5', true),
            new TransferSet('awa_stromversorgungssicherheit_pub_ebene_7.sql', 'awa_stromversorgungssicherheit_pub.netzbetreiber_ebene_7', true)
    ];
}

task exportMgdmNetzgebiete(type: Ili2pgExport, dependsOn: 'transferNetzgebiete') {
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    models = "SupplySecurity_RuledAreas_V1_2"
    if (findProperty('ili2dbModeldir')) modeldir = ili2dbModeldir
    dbschema = "awa_stromversorgungssicherheit"
    dataFile = file(mgdmNetzgebieteXtfFileName)
    disableValidation = true
}

task validateMgdmNetzgebiete(type: IliValidator, dependsOn: 'exportMgdmNetzgebiete') {
    dataFiles = [file(mgdmNetzgebieteXtfFileName)]
    if (findProperty('ilivalidatorModeldir')) modeldir = ilivalidatorModeldir
    logFile = "ilivalidator.log"
    failOnError = true
}

task zipMgdmNetzgebiete(type: Zip, dependsOn: 'validateMgdmNetzgebiete'){
    from pathToTempFolder
    from "."
    include mgdmNetzgebieteXtfFileName
    archiveName mgdmNetzgebieteZipFileName
    destinationDir(file(pathToTempFolder))
}

task uploadMgdmNetzgebiete(dependsOn: 'zipMgdmNetzgebiete') {
    doLast {
        def response = ["curl", "-u", aiLogin, "-F", "topic=stromversorgungssicherheit_netzgebiete_v1_2", "-F",
                        "lv95_file=@" + Paths.get(pathToTempFolder.toString(), mgdmNetzgebieteZipFileName), "-F", "publish=true",
                        "https://" + aiServer + "/data_agg/interlis/import"].execute().text
        println(response)
        if (response.contains("false") || response == null || response.trim().isEmpty()) {
            throw new GradleException()
        }
    }
}
