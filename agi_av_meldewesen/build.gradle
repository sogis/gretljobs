import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.*
import java.nio.file.Paths

apply plugin: 'ch.so.agi.gretl'
apply plugin: "org.hidetake.ssh"

defaultTasks 'update_manual_records'

def pathToTempFolder = System.getProperty("java.io.tmpdir")
def downloadFolder = Paths.get(pathToTempFolder, "download").toFile()
def transformFolder = Paths.get(pathToTempFolder, "transform").toFile()

println downloadFolder
println transformFolder

if (!downloadFolder.exists()) {
    downloadFolder.mkdirs()
}

if (!transformFolder.exists()) {
    transformFolder.mkdirs()
}

def remoteDirectory = "sgv-gemdat/sgv/ech0132-test/."
def bucketSuffix = '-undefined'
    if ( gretlEnvironment == 'production' ) { 
        bucketSuffix = ''
        remoteDirectory = "sgv-gemdat/sgv/ech0132/."
    } else if ( gretlEnvironment == 'integration' || gretlEnvironment == 'development' ) { 
        bucketSuffix = '-' + gretlEnvironment.substring(0, 3)
    } else {
        bucketSuffix = '-' + gretlEnvironment
    }

def sgvMeldungenBucket = "ch.so.agi.sgv-meldungen$bucketSuffix"

remotes {
    sftpServer {
        host = "${sftpServerSogis}"
        user = "${sftpUserSogisGemdat}"
        //identity = file('/Users/stefan/keys/sgv/id_rsa')
        identity = file('/home/gradle/.sshkeys/id_rsa')
        // Skip host key checking outside the following environments:
        if ( !(gretlEnvironment in ['test', 'integration', 'production']) ) { // uses the Groovy membership operator
            knownHosts = allowAnyHosts
        }
    }
}

tasks.register('downloadFromSgv') {
    description = "Download files from SFTP server"
    doLast {
        ssh.run {
            session(remotes.sftpServer) {
                get from: remoteDirectory, into: downloadFolder, filter: { it.name.toLowerCase().endsWith("xml") }
            }
        }
        println "Files downloaded from SFTP server"
    }
}

tasks.register('uploadXmlToS3', S3Upload) {
    dependsOn 'downloadFromSgv'
    description = "Original XML-Dateien (eCH-0132-Meldungen) nach S3 hochladen."
    accessKey = awsAccessKeyAgi
    secretKey = awsSecretAccessKeyAgi
    sourceDir = file(downloadFolder)
    endPoint = "https://s3.eu-central-1.amazonaws.com"
    region = "eu-central-1"
    bucketName = sgvMeldungenBucket
    acl = "private"
}

tasks.register('transform', XslTransformer) {
    dependsOn 'uploadXmlToS3'
    description = "Transformiert die eCH-0132-Meldungen nach INTERLIS."
    xslFile = file("./xml2xtf.xsl")
    xmlFile = fileTree(downloadFolder).include { it.file.name.toLowerCase().endsWith("xml") }
    outDirectory = file(transformFolder)
}

tasks.register('uploadXtfToS3', S3Upload) {
    dependsOn 'transform'
    description = "XTF-Dateien nach S3 hochladen."
    accessKey = awsAccessKeyAgi
    secretKey = awsSecretAccessKeyAgi
    sourceDir = file(transformFolder)
    endPoint = "https://s3.eu-central-1.amazonaws.com"
    region = "eu-central-1"
    bucketName = sgvMeldungenBucket
    acl = "private"
}

tasks.register('importData', Ili2pgReplace) {
    dependsOn 'uploadXtfToS3'
    description = 'Import der Meldungen als XTF in die Datenbank'
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    dbschema = "agi_av_meldewesen_import_v1"
    models = 'SO_AGI_SGV_Meldungen_20221109'
    dataFile = fileTree(dir: transformFolder, include: '*.xtf')
    dataset = dataFile
    disableValidation = false
}

tasks.register('transfer', SqlExecutor) {
    dependsOn 'importData'
    description = 'Kopiert die (neuen) Meldungen in das Work-Schema'
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    sqlFiles = ['transfer_to_work.sql']
}

/*
E-Mail an SGV, ob das korrekt ist, dass kein EGRID geschickt wird oder versehen.
Vielleicht geht SQL eleganter (z.B. alles in transfer-Query). Bei mir (mit "OR-Bedingung")
wird es brutal langsam.
*/
tasks.register('updateLage', SqlExecutor) {
    dependsOn 'transfer'
    description = 'Updated die Lage für Meldungen, die keinen EGRID haben und somit keine Lage.'
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    sqlFiles = ['update_lage.sql']
}

/*
Default-Value geht nicht, weil es eine SOOID ist (mit "_" als Prefix). SOOID o.ä. muss es 
sein, weil ich so die Message-Id verwenden kann. Aber, ob wirklich notwendig (message id),
weiss ich nicht. Vielleicht auch premature of optimization is evil Moment, den ich 
mitschleppe.
*/
tasks.register('update_manual_records', SqlExecutor) {
    dependsOn 'updateLage'
    description = 'Inserted eine OID in die t_ili_tid-Spalte und weitere leere Attribute für Punkte, die vom Geometer erfasst wurden.'
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    sqlFiles = ['update_manual_records.sql']
}



// Löschen der Daten auf dem FTP-Server.
// Lokales Löschen nicht notwendig, da im Container.
tasks.register('deleteDataOnFtp') {
    description = "Delete files on SFTP server"
    doLast {
        ssh.run {
            session(remotes.sftpServer) {
                fileTree(downloadFolder).matching {
                    include "**/*.xml"
                }.each {
                    def fileName = remoteDirectory + it.getName()
                    println "Deleting: " + fileName
                    // ************ TODO !!!!!!!!!! -> det ret = remove fileName
                    def ret = remove '/remote/file'
                    if (!ret) {
                        throw new GradleException("could not delete: " + fileName)
                    }
                }
            }
        }
        println "Files deleted on SFTP server"
    }
}
