import ch.so.agi.gretl.api.TransferSet
import ch.so.agi.gretl.tasks.*
import java.nio.file.Paths
import de.undercouch.gradle.tasks.download.Download



apply plugin: "de.undercouch.download"
apply plugin: 'ch.so.agi.gretl'


defaultTasks 'removeFiles'

def pathToTempFolder = System.getProperty("java.io.tmpdir")
def pathToUnzipFolder = Paths.get(pathToTempFolder, 'unzip_data')
def pathToDataZip = Paths.get(pathToTempFolder, "ortschaftenverzeichnis_plz_2056_de.itf.zip")
def xtfFilePath = Paths.get(pathToUnzipFolder.toString(), 'PLZO_INTERLIS_LV95', 'PLZO_ITF_LV95.itf')


task download(type: Download){
    description = "Download plz_ortschaften from swisstopo"
    doLast {
        println "File downloaded to: " + pathToTempFolder
    }
    src 'https://data.geo.admin.ch/ch.swisstopo-vd.ortschaftenverzeichnis_plz/ortschaftenverzeichnis_plz/ortschaftenverzeichnis_plz_2056_de.itf.zip'
    dest pathToTempFolder
    overwrite true
}


task unzipData(type: Copy, dependsOn: 'download'){
    description = "Unzip Data.zip."
    doLast {
        println "File unzipped to directory: " + pathToUnzipFolder
    }
    from zipTree(pathToDataZip)
    into file(pathToUnzipFolder)
    include "**/*.itf"
}

task dbImport(type: Ili2pgImport, dependsOn: 'unzipData'){
    description = "Import plz_ortschaften-Data into Edit database"
    doLast {
        println "Data imported into db: " + dbUriEdit
    }
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    dbschema = "agi_plz_ortschaften"
    models = "PLZOCH1LV95D"
    disableValidation = false
    dataFile = file(xtfFilePath)
    deleteData = true
    strokeArcs = true
}

task convertDataEdit(type: SqlExecutor, dependsOn: 'dbImport'){
    description = "Convert plz_ortschaften-Data to Edit Schema"
    doLast {
        println "plz_ortschaften-Data are converted to Edit Schema."
    }
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    sqlFiles = [
        'delete_ortschaft.sql',
        'delete_postleitzahl.sql',
        'insert_ortschaft.sql',
        'insert_postleitzahl.sql'
    ]
}

task copyDataPub(type: Db2Db, dependsOn: 'convertDataEdit'){
    description = "Copy plz_ortschaften-Data to Pub Schema"
    doLast {
        println "plz_ortschaften-Data are copied to Pub Schema."
    }
    sourceDb = [dbUriEdit, dbUserEdit, dbPwdEdit]
    targetDb = [dbUriPub, dbUserPub, dbPwdPub]
    transferSets = [
        new TransferSet('plzortschaften_ortschaft.sql', 'agi_plz_ortschaften_pub.plzortschaften_ortschaft', true),
        new TransferSet('plzortschaften_postleitzahl.sql', 'agi_plz_ortschaften_pub.plzortschaften_postleitzahl', true)
    ];
}

task removeFiles(type: Delete, dependsOn: 'copyDataPub') {
    description = "Remove Files"
    doLast {
        println "Files removed."
    }
    delete file(pathToUnzipFolder), file(pathToDataZip)
}
