/*
Publiziert die AVT Ausnahmetransportrouten nach geodienste.ch
*/

import java.nio.file.Files
import java.nio.file.Paths
import java.io.File
import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.TransferSet
import de.undercouch.gradle.tasks.download.Download

defaultTasks 'uploadMgdm'

/*
Package and upload to the KKGEO AI
*/
def uploadFileName = 'uploadFile'
def exportFileName = 'data.xtf'
def pathToUploadFolder = 'upload'
def zipName = 'iliexport.zip'
def pathToTempFolder = System.getProperty("java.io.tmpdir")
def validateFile = pathToTempFolder + "/" + exportFileName
def pathToCatalogFile = "https://models.geo.admin.ch/ASTRA/ExceptionalLoadsRoute_Catalogue_V1.xml"
def catalogFileName = 'ExceptionalLoadsCatalogues_V1.TypeOfRouteCatalogue.xml'
def validateCatalogFile = pathToTempFolder + "/" + catalogFileName

task download(type: Download) {
    description = "Download Catalog https://models.geo.admin.ch/ASTRA/ExceptionalLoadsRoute_Catalogue_V1.xml"
    src "https://models.geo.admin.ch/ASTRA/ExceptionalLoadsRoute_Catalogue_V1.xml"
    dest pathToTempFolder + catalogFileName
    overwrite true
     doLast {
        println "File downloaded to: " + pathToTempFolder
    }
}

task copyXtfFile(type: Copy) {
    dependsOn "download"
    from Paths.get(pathToUploadFolder)
    into Paths.get(pathToTempFolder)
    include uploadFileName
    rename uploadFileName, exportFileName
}

/**
 * Validiere den Datensatz.
 */
task validateData(type: IliValidator){
    dependsOn "copyXtfFile"
    description "Validiere den importierten Datensatz"
    dataFiles = [validateFile, validateCatalogFile]
    if (findProperty('ilivalidatorModeldir')) modeldir = ilivalidatorModeldir
    logFile = "validation.log"
    allObjectsAccessible = true
    forceTypeValidation = true
    failOnError = true
    
}

task zipMgdm(type: Zip) {
    dependsOn "validateData"
    archiveName = zipName
    destinationDir = file(pathToTempFolder)
    from Paths.get(pathToTempFolder)
    include exportFileName
}

task uploadMgdm (dependsOn: zipMgdm) {
    description = "LÃ¤dt die Daten in die Aggregationsinfrastruktur hoch."

    def aiLogin = aiUser + ":" + aiPwd
    def serverUrl = "https://" + aiServer + "/data_agg/interlis/import"
    def zipFilePath = Paths.get(pathToTempFolder, zipName)

    doLast {    
        def response = ["curl", "-u", aiLogin, "-F", "topic=kantonale_ausnahmetransportrouten", "-F",
                        "lv95_file=@" + zipFilePath, "-F", "publish=true", serverUrl
                        ].execute().text
        println(response)
    }
}
