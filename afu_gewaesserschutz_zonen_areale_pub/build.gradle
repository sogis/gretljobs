import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.TransferSet
import java.nio.file.Paths

apply plugin: 'ch.so.agi.gretl'

defaultTasks 'publiedit', 'deleteData_staging_pub', 'uploadXtf'

def fileName = "afu_gewaesserschutz_zonen_areale.xtf"
def fileNameZip = "afu_gewaesserschutz_zonen_areale.zip"
def fileNamePub = "afu_gewaesserschutz_zonen_areale_pub.xtf"

def aiLogin = aiUser + ":" + aiPwd

task publiedit(type: Publisher){
  dataIdent = "ch.so.afu.gewaesserschutz.relational"
  database = [dbUriEdit,dbUserEdit,dbPwdEdit]
  dbSchema = "afu_gewaesserschutz_zonen_areale_v1"
  modelsToPublish = "PlanerischerGewaesserschutz_LV95_V1_1"

  target = ["sftp://$sftpServerSogis/$sftpUserSogis", sftpUserSogis, sftpPwdSogis]
  kgdiService = [simiMetadataServiceUrl, simiMetadataServiceUser, simiMetadataServicePwd]
  kgdiTokenService = [simiTokenServiceUrl, simiTokenServiceUser, simiTokenServicePwd]
  grooming = new File(file(projectDir).getParentFile(), "publisher_grooming.json")
  if (findProperty('ilivalidatorModeldir')) modeldir = ilivalidatorModeldir
}

task transferGewaesserschutz_staging(type: Db2Db, dependsOn: 'publiedit') {
    sourceDb = [dbUriEdit, dbUserEdit, dbPwdEdit]
    targetDb = [dbUriPub, dbUserPub, dbPwdPub]
    transferSets = [
            new TransferSet('gewaesserschutz_schutzzone_staging.sql', 'afu_gewaesserschutz_staging_v1.gewaesserschutz_schutzzone', true),
            new TransferSet('gewaesserschutz_schutzareal_staging.sql', 'afu_gewaesserschutz_staging_v1.gewaesserschutz_schutzareal', true),
    ];
}

task publipub(type: Publisher, dependsOn: 'transferGewaesserschutz_staging'){
  dataIdent = "ch.so.afu.gewaesserschutz"
  userFormats = true
  database = [dbUriPub,dbUserPub,dbPwdPub]
  dbSchema = "afu_gewaesserschutz_pub_v1"
  modelsToPublish = "SO_AfU_Gewaesserschutz_Publikation_20210303"

  target = ["sftp://$sftpServerSogis/$sftpUserSogis", sftpUserSogis, sftpPwdSogis]
  kgdiService = [simiMetadataServiceUrl, simiMetadataServiceUser, simiMetadataServicePwd]
  kgdiTokenService = [simiTokenServiceUrl, simiTokenServiceUser, simiTokenServicePwd]
  grooming = new File(file(projectDir).getParentFile(), "publisher_grooming.json")
  if (findProperty('ilivalidatorModeldir')) modeldir = ilivalidatorModeldir

}

task exportData (type: Ili2pgExport, dependsOn: 'publipub') {
    description = "Exportiert die umgebauten Daten aus dem Schema afu_gewaesserschutz_staging in ein INTERLIS-Datei."
    database = [dbUriPub, dbUserPub, dbPwdPub]
    dbschema = 'afu_gewaesserschutz_staging_v1'
    models = 'SO_AfU_Gewaesserschutz_Publikation_20220817'
    dataFile= file(fileNamePub)
    disableValidation = true
}

task transferGewaesserschutz_pub(type: Db2Db, dependsOn: 'exportData') {
    sourceDb = [dbUriPub, dbUserPub, dbPwdPub]
    targetDb = [dbUriPub, dbUserPub, dbPwdPub]
    transferSets = [
            new TransferSet('gewaesserschutz_schutzzone.sql', 'afu_gewaesserschutz_pub_v1.gewaesserschutz_schutzzone', true),
            new TransferSet('gewaesserschutz_schutzareal.sql', 'afu_gewaesserschutz_pub_v1.gewaesserschutz_schutzareal', true),
    ];
}

task zipXtf(type: Zip, dependsOn: 'transferGewaesserschutz_pub'){
    from "."
    include fileName
    archiveName fileNameZip
    destinationDir(file("."))
}

/* Falls man auf geodienste.ch alles löschen will/muss (siehe zweiter Abschnitt), muss man
* einmalig replace_all=true setzen. Curl-Befehl kann natürlich auch lokal
* ausgeführt werden.
*
* Achtung: Die Dateinamen müssen immer gleich sein. Diese werden als 
* dataset-Name verwendet. Will man die Namen ändern, siehe erster Abschnitt
* des Kommentars.
*/
task uploadXtf(dependsOn: 'zipXtf') {
    doLast {
        def response = ["curl", "-u", aiLogin, "-F", "topic=planerischer_gewaesserschutz", "-F",
                        "lv95_file=@" + Paths.get(".", fileNameZip), "-F", "publish=true", "-F", "replace_all=false",
                        "https://" + aiServer + "/data_agg/interlis/import"].execute().text
        println(response)
        if (response.contains("false") || response == null || response.trim().isEmpty()) {
            throw new GradleException()
        }
    }
}

task deleteData_staging_pub (type: SqlExecutor, dependsOn: 'transferGewaesserschutz_pub') {
    description = "Löscht/leert die Daten aus dem Schema afu_gewaesserschutz_staging"
    database = [dbUriPub, dbUserPub, dbPwdPub]
    sqlFiles = ["delete_afu_gewaesserschutz_staging.sql"]
}
