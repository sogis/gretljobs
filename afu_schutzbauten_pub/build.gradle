import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.TransferSet

apply plugin: 'ch.so.agi.gretl'

defaultTasks 'publishPubData'

/**
 * Sammle alle Objekte aus der Erfassungsdatenbank nach Geometrie getrennt
 * und schreibe diese ins Publikationsmodell.
 */
task transferSchutzbautenobjekte(type: Db2Db){
    description = "Baue die Daten vom Erfassungsmodell ins Publikationsmodell um"
    sourceDb = [dbUriEdit, dbUserEdit, dbPwdEdit]
    targetDb = [dbUriPub, dbUserPub, dbPwdPub]
    transferSets = [
        // Punkt Objekte
        new TransferSet(
            'select_schutzbautenobjekt_punkt.sql',
            'afu_schutzbauten_pub_v1.schutzbautenobjekt_punkt',
            true
        ),
        // Linien Objekte
        new TransferSet(
            'select_schutzbautenobjekt_linie.sql',
            'afu_schutzbauten_pub_v1.schutzbautenobjekt_linie',
            true
        ),
        // Fl√§chen Objekte
        new TransferSet(
            'select_schutzbautenobjekt_flaeche.sql',
            'afu_schutzbauten_pub_v1.schutzbautenobjekt_flaeche',
            true
        )
    ]
}

/**
 * Exportiere die Daten aus der edit Datenbank nach INTERLIS bzw.
 * GIS Dateien und leite diese an die KGDI weiter
 */
task publishPubData(type: Publisher){
    dependsOn 'transferSchutzbautenobjekte'
    database = [dbUriPub, dbUserPub, dbPwdPub]
    dataIdent = "ch.so.afu.schutzbauten"
    dbSchema = "afu_schutzbauten_pub_v1"
    modelsToPublish = "SO_AFU_Schutzbauten_Publikation_20231212"
    userFormats = true
    target = [sftpUrlSogis, sftpUserSogis, sftpPwdSogis]
    kgdiService = [simiMetadataServiceUrl, simiMetadataServiceUser, simiMetadataServicePwd]
    kgdiTokenService = [simiTokenServiceUrl, simiTokenServiceUser, simiTokenServicePwd]
    if (findProperty('ilivalidatorModeldir')) modeldir = ilivalidatorModeldir
}