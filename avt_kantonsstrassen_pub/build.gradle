import java.nio.file.Files
import java.nio.file.Paths
import java.io.File
import ch.so.agi.gretl.tasks.*

defaultTasks 'refreshSolr'
 
def shpNameAchsen = 'achsen.shp'
def shpNamePunkte = 'bezugspunkte.shp'
def pathToShpFolder = Paths.get(System.getProperty("java.io.tmpdir"), "avt_achsen").toString() 
def uploadFile = 'upload/uploadFile'

task unpackShapes(type: Copy) {
    from zipTree(uploadFile)
    into pathToShpFolder
}

task validate(type: ShpValidator, dependsOn: unpackShapes){
    models = "SO_AVT_Achsen_Publikation_20200707"
    dataFiles = [Paths.get(pathToShpFolder, shpNamePunkte),Paths.get(pathToShpFolder, shpNameAchsen)]
} 

task deleteAllFeatures(type: SqlExecutor, dependsOn: validate){
    database = [dbUriPub, dbUserPub, dbPwdPub]
    sqlFiles = ['delete_achsen_u_punkte.sql']
}

task importAchsen(type: ShpImport, dependsOn: deleteAllFeatures){
    database = [dbUriPub, dbUserPub, dbPwdPub]
    schemaName = "avt_kantonsstrassen_pub"
    tableName = "achse"
    dataFile = Paths.get(pathToShpFolder, shpNameAchsen) 
    encoding = "UTF-8"
}

task importPunkte(type: ShpImport, dependsOn: importAchsen){
    database = [dbUriPub, dbUserPub, dbPwdPub]
    schemaName = "avt_kantonsstrassen_pub"
    tableName = "bezugspunkt"
    dataFile = Paths.get(pathToShpFolder, shpNamePunkte) 
    encoding = "UTF-8"
}
task publipub(type: Publisher, dependsOn: 'importPunkte'){
    dataIdent = "ch.so.avt.kantonsstrassen"
    userFormats = true

    database = [dbUriPub,dbUserPub,dbPwdPub]
    dbSchema = "avt_kantonsstrassen_pub"
    modelsToPublish = "SO_AVT_Kantonsstrassen_Publikation_20200707"

    target = [sftpUrlSogis, sftpUserSogis, sftpPwdSogis]
    kgdiService = [simiMetadataServiceUrl, simiMetadataServiceUser, simiMetadataServicePwd]
    kgdiTokenService = [simiTokenServiceUrl, simiTokenServiceUser, simiTokenServicePwd]
    grooming = new File(file(projectDir).getParentFile(), "publisher_grooming.json")
    if (findProperty('ilivalidatorModeldir')) modeldir = ilivalidatorModeldir
}

task refreshSolr(type:Exec, dependsOn:'publipub') {
        commandLine 'curl', '-i', '--max-time', '5', solrIndexupdaterBaseUrl + '/queue?ds=ch.so.avt.kantonsstrassen_achsen,ch.so.avt.kantonsstrassen_bezugspunkte'
}

