import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.TransferSet

import java.nio.file.Paths

defaultTasks "publishPub"

def tmpDir = buildDir.toString()
def xtfPath = tmpDir + "/data.xtf"

task exportXtf(type: Ili2pgExport){
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    dbschema = "avt_kunstbauten_staging_v1"
    disableValidation = true
    dataFile = xtfPath
    models = "SO_AVT_Kunstbauten_Publikation_20220207"
}

task validate(type: IliValidator, dependsOn: exportXtf){
    dataFiles = [xtfPath]
    if (findProperty('ilivalidatorModeldir')) modeldir = ilivalidatorModeldir
}

task writeToPub(type: Db2Db, dependsOn: validate){ 
    sourceDb = [dbUriEdit, dbUserEdit, dbPwdEdit]
    targetDb = [dbUriPub, dbUserPub, dbPwdPub]
    transferSets = [
        new TransferSet('kunstbaute.sql', 'avt_kunstbauten_pub_v1.kunstbaute', true)
    ];
}

task publishPub(type: Publisher, dependsOn: writeToPub){
    database = [dbUriPub, dbUserPub, dbPwdPub]
    dataIdent = "ch.so.avt.kunstbauten"
    dbSchema = "avt_kunstbauten_pub_v1"
    modelsToPublish = "SO_AVT_Kunstbauten_Publikation_20220207"
    userFormats = true
    target = [sftpUrlSogis, sftpUserSogis, sftpPwdSogis]
    kgdiService = [simiMetadataServiceUrl, simiMetadataServiceUser, simiMetadataServicePwd]
    kgdiTokenService = [simiTokenServiceUrl, simiTokenServiceUser, simiTokenServicePwd]
    grooming = new File(file(projectDir).getParentFile(), "publisher_grooming.json")
    if (findProperty('ilivalidatorModeldir')) modeldir = ilivalidatorModeldir
}
