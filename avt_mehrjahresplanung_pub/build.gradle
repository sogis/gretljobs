import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.TransferSet

apply plugin: 'ch.so.agi.gretl'

defaultTasks 'transferData'

task replaceEnumBetriebs(){
    // In einer INTERLIS Aufzählung können keine Bindestriche verwendet werden
    description = "Ersetzt 'Betriebs- und Gestaltungskonzept' zu 'Betriebs und Gestaltungskonzept."
    doLast {
        ant.replace(
            file: csvFilename,
            token: "Betriebs- und Gestaltungskonzept",
            value: "Betriebs_und_Gestaltungskonzept")
    }
}

task validateInputFile(type: CsvValidator) {
    dependsOn replaceEnumBetriebs
    models = "SO_AVT_Mehrjahresplanung_20250616"
    dataFiles = [ csvFilename ]
    valueSeparator = ';'
    firstLineIsHeader = true
    encoding = "UTF-8"
    configFile = "csv_file_validation.ini"
}

task replaceStartHeader(){
    dependsOn validateInputFile
    description = "Ersetzt start zu astart."
    doLast {
        ant.replaceregexp( // Verwende replace mit RegExp, damit der Ausdruck
            // nur einmal ersetzt wird
            file: csvFilename,
            match: "start",
            replace: "astart")
    }
}

task replacePhaseHeader(){
    dependsOn validateInputFile
    description = "Ersetzt phase zu aphase."
    doLast {
        ant.replaceregexp(
            file: csvFilename,
            match: "phase",
            replace: "aphase")
    }
}

task emptyTable(type: SqlExecutor) {
    dependsOn replaceStartHeader, replacePhaseHeader
    description = "Leere die Datenbanktabelle"
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    sqlFiles = ['delete_projekte.sql']
}

task importFile(type: CsvImport) {
    dependsOn emptyTable
    description = "Importiere die CSV-Datei"
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    schemaName = "avt_mehrjahresplanung_v2"
    tableName = "projekte_projekt"
    valueSeparator = ';'
    dataFile = csvFilename
    encoding = "UTF-8"
}

task copyKantonsstrassenData(type: Db2Db) {
    dependsOn importFile
    description = "Kopiere die Daten der Kantonsstrassen aus der Publikationsdatenbank ins Erfassungsschema"
    sourceDb = [dbUriPub, dbUserPub, dbPwdPub]
    targetDb = [dbUriEdit, dbUserEdit, dbPwdEdit]
    transferSets = [
        new TransferSet('select_achsen.sql', 'avt_mehrjahresplanung_v2.kantonsstrassen_achse', true),
        new TransferSet('select_bezugspunkte.sql', 'avt_mehrjahresplanung_v2.kantonsstrassen_bezugspunkt', true)
    ]
}

task setAssociations(type: SqlExecutor) {
    dependsOn copyKantonsstrassenData
    description = "Füge die Beziehungen von Projekte zu den Bezugspunkten"
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    sqlFiles = ['update_projekt.sql']
}

task handleObsoleteGeometries(type: SqlExecutor) {
    dependsOn setAssociations
    description = "Setze ein Obsolet-Datum und lösche Geometrien älter als sechs Monate"
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    sqlFiles = ['update_projektgeometrie.sql']
}

task validateData(type: Ili2pgValidate) {
    dependsOn handleObsoleteGeometries
    description = "Validiere die Erfassungsdaten"
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    models = "SO_AVT_Mehrjahresplanung_20250616"
    dbschema = "avt_mehrjahresplanung_v2"
}

task transferData(type: Db2Db) {
    dependsOn validateData
    description = "Baue die Daten in das Publikationsmodell um"
    sourceDb = [dbUriEdit, dbUserEdit, dbPwdEdit]
    targetDb = [dbUriPub, dbUserPub, dbPwdPub]
    transferSets = [
        new TransferSet('select_projekte_und_geometrie.sql', 'avt_mehrjahresplanung_pub_v2.projekt', true)
    ]
}
