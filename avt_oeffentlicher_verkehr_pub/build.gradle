import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.TransferSet

apply plugin: 'ch.so.agi.gretl'

defaultTasks 'publishPub', 'updateSearchIndex'

task transferAvtOeffentlicherVerkehrPub(type: Db2Db) {
    sourceDb = [dbUriEdit, dbUserEdit, dbPwdEdit]
    targetDb = [dbUriPub, dbUserPub, dbPwdPub]
    transferSets = [
            new TransferSet('avt_oeffentlicher_verkehr_pub_haltestellen.sql', 'avt_oeffentlicher_verkehr_pub.oeffntlchr_vrkehr_haltestelle', true),
            new TransferSet('avt_oeffentlicher_verkehr_pub_netz.sql', 'avt_oeffentlicher_verkehr_pub.oeffntlchr_vrkehr_netz', true)
    ];
}
task publishPub(type: Publisher, dependsOn: 'transferAvtOeffentlicherVerkehrPub'){
    database = [dbUriPub, dbUserPub, dbPwdPub]
    dataIdent = "ch.so.avt.oev"
    dbSchema = "avt_oeffentlicher_verkehr_pub"
    modelsToPublish = "SO_AVT_Oeffentlicher_Verkehr_20210205"
    userFormats = true
    target = [sftpUrlSogis, sftpUserSogis, sftpPwdSogis]
    kgdiService = [simiMetadataServiceUrl, simiMetadataServiceUser, simiMetadataServicePwd]
    kgdiTokenService = [simiTokenServiceUrl, simiTokenServiceUser, simiTokenServicePwd]
    grooming = new File(file(projectDir).getParentFile(), "publisher_grooming.json")
    if (findProperty('ilivalidatorModeldir')) modeldir = ilivalidatorModeldir
}

task updateSearchIndex(type: SqlExecutor){
    mustRunAfter 'publishPub'
    def layerName = 'ch.so.avt.oev.haltestellen'
    def sqlFileName = 'searchindex_' + layerName + '.sql'

    database = [dbUriPub, dbUserPub, dbPwdPub]
    def dbSearchSchema = dbSearchSchemaPub
    String layerNameString = "'${layerName}'"

    sqlParameters = [
            [layername: layerNameString, db_schema: dbSearchSchema]
    ]
    sqlFiles = [
            '../searchindex_deleteFeatures.sql',
            sqlFileName
    ]
    doFirst {
        println 'layer name: ' + layerName
    }
}
