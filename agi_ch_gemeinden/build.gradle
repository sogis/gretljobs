import ch.so.agi.gretl.api.TransferSet
import ch.so.agi.gretl.tasks.*
import java.nio.file.Paths
import de.undercouch.gradle.tasks.download.Download



apply plugin: "de.undercouch.download"
apply plugin: 'ch.so.agi.gretl'


defaultTasks 'transferDataToPubDb'

def pathToTempFolder = System.getProperty("java.io.tmpdir")
def pathToUnzipFolder = Paths.get(pathToTempFolder, 'unzip_data')
def pathToDataZip = Paths.get(pathToTempFolder, 'swissboundaries3d_2024-01_2056_5728.xtf.zip')
def xtfFilePath = Paths.get(pathToUnzipFolder.toString(),'swissBOUNDARIES3D_1_5.xtf')


task download(type: Download){
    description = "Download swissBoundaries3D from swisstopo"
    doLast {
        println "File downloaded to: " + pathToTempFolder
    }
    src 'https://data.geo.admin.ch/ch.swisstopo.swissboundaries3d/swissboundaries3d_2024-01/swissboundaries3d_2024-01_2056_5728.xtf.zip'
    dest pathToTempFolder
    overwrite true
}


task unzipSwissBoundaries3D(type: Copy, dependsOn: 'download'){
    description = "Unzip swissBOUNDARIES3D_1_5.xtf from swissboundaries3d_2024-01_2056_5728.xtf.zip."
    doLast {
        println "File unzipped to directory: " + pathToUnzipFolder
    }
    from zipTree(pathToDataZip)
    into file(pathToUnzipFolder)
    include "**/*.xtf"
}


task dbImport(type: Ili2pgImport, dependsOn: 'unzipSwissBoundaries3D'){
    description = "Import swissBoundaries3D-Data into database"
    doLast {
        println "Data imported into db: " + dbUriEdit
    }
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    dbschema = "agi_swissboundaries3d_v2"
    models = "swissBOUNDARIES3D_ili2_LV95_V1_5"
    disableValidation = false
    dataFile = file(xtfFilePath)
    deleteData = true
    strokeArcs = true

    finalizedBy 'removeFiles'
}

