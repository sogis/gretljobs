import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.TransferSet

apply plugin: 'ch.so.agi.gretl'
apply plugin: 'org.hidetake.ssh'

defaultTasks "upload"

// Definition von Variablen
def tmpDir = buildDir.toString()
def xtfPath = tmpDir + "/sein_strukturdaten.xtf"

// Definition des SFTP-Servers für den Datenupload
remotes {
    sftpServer {
        host = "${sftpServerSEinApp}"
        user = "${sftpUserSEinApp}"
        identity = file('/home/gradle/.ssh/id_rsa')
        // Skip host key checking outside the following environments:
        if ( !(gretlEnvironment in ['test', 'integration', 'production']) ) {  // uses the Groovy membership operator
            knownHosts = allowAnyHosts
        }
    }
}

// Extension für die UUID-Erstellung aktivieren (uuid-ossp)
task createExtension(type: SqlExecutor){
    database = [dbUriProcessing, dbUserProcessing, dbPwdProcessing]
    sqlFiles = ['create_extension.sql']
}

// $td models zeilenumbruch
// Schema "import" für Quelldaten in processing DB anlegen
task createImportSchema(type: Ili2pgImportSchema, dependsOn: createExtension){
    database = [dbUriProcessing, dbUserProcessing, dbPwdProcessing]
    models = "SO_AGI_MOpublic_20240711;SO_AGI_GWR_Publikation_20241118;SO_ARP_STATPOP_STATENT_20221114;SO_ARP_Auswertungen_Nutzungsplanung_Publikation_20210126;SO_ARP_Nutzungsplanung_Publikation_20201005;SO_Hoheitsgrenzen_Publikation_20170626;SO_ARP_SEin_Konfiguration_20250115"
    dbschema = "import"
    nameByTopic = true
    beautifyEnumDispName = true
    strokeArcs = true
    createEnumTabs = false
    createNumChecks = true
    createFk = true
    createFkIdx = true
    createGeomIdx = true
    createMetaInfo = true
    createBasketCol = false
    createUnique = true
    smart2Inheritance = true
}

// Schema "export" für Ausgabedaten in processing DB anlegen
task createExportSchema(type: Ili2pgImportSchema, dependsOn: createImportSchema){
    database = [dbUriProcessing, dbUserProcessing, dbPwdProcessing]
    models = "SO_ARP_SEin_Strukturdaten_Publikation_20250407"
    dbschema = "export"
    nameByTopic = true
    beautifyEnumDispName = true
    strokeArcs = true
    createEnumTabs = false
    createNumChecks = true
    createFk = true
    createFkIdx = true
    createGeomIdx = true
    createMetaInfo = true
    createBasketCol = false
    createUnique = true
    smart2Inheritance = true
}

// Quelldaten tabellenweise aus Pub nach Processing kopieren
task copyDataFromPub(type: Db2Db, dependsOn: createExportSchema){ 
    sourceDb = [dbUriPub, dbUserPub, dbPwdPub]
    targetDb = [dbUriProcessing, dbUserProcessing, dbPwdProcessing]
    transferSets = [
        new TransferSet('import_to_processing/import_bauzonenstatistik_liegenschaft_nach_bebauungsstand.sql', 'import.bauzonenstatistik_liegenschaft_nach_bebauungsstand', true),
        new TransferSet('import_to_processing/import_nutzungsplanung_grundnutzung.sql', 'import.nutzungsplanung_grundnutzung', true),
        new TransferSet('import_to_processing/import_mopublic_bodenbedeckung.sql', 'import.mopublic_bodenbedeckung', true),
        new TransferSet('import_to_processing/import_mopublic_grundstueck.sql', 'import.mopublic_grundstueck', true),
        new TransferSet('import_to_processing/import_gwr_gebaeude.sql', 'import.gwr_gebaeude', true),
        new TransferSet('import_to_processing/import_gwr_wohnung.sql', 'import.gwr_wohnung', true),
        new TransferSet('import_to_processing/import_hoheitsgrenzen_gemeindegrenze.sql', 'import.hoheitsgrenzen_gemeindegrenze', true)
    ];
}

// Quelldaten tabellenweise aus Edit nach Processing kopieren
task copyDataFromEdit(type: Db2Db, dependsOn: copyDataFromPub){ 
    sourceDb = [dbUriEdit, dbUserEdit, dbPwdEdit]
    targetDb = [dbUriProcessing, dbUserProcessing, dbPwdProcessing]
    transferSets = [
        new TransferSet('import_to_processing/import_statpop.sql', 'import.statpop_statent_statpop', true),
        new TransferSet('import_to_processing/import_statent.sql', 'import.statpop_statent_statent', true),
        new TransferSet('import_to_processing/import_grundlagen_gemeinde.sql', 'import.grundlagen_gemeinde', true)
    ];
}

// $td Zusammenfassen in einen Executor, da es immer um Raumeinheit geht?
// Raumeinheit Parzelle aufbereiten
task processParzellen(type: SqlExecutor, dependsOn: copyDataFromEdit) {
    database = [dbUriProcessing, dbUserProcessing, dbPwdProcessing]
    sqlFiles = ['parzelle.sql']
}

// Raumeinheit Zonentyp aufbereiten (bedingt parzelle.sql)
task processZonentypen(type: SqlExecutor, dependsOn: processParzellen) {
    database = [dbUriProcessing, dbUserProcessing, dbPwdProcessing]
    sqlFiles = ['zonentyp.sql']
}

// Raumeinheit Zonenschild aufbereiten (bedingt zonentyp.sql und somit parzelle.sql)
task processZonenschilde(type: SqlExecutor, dependsOn: processZonentypen) {
    database = [dbUriProcessing, dbUserProcessing, dbPwdProcessing]
    sqlFiles = ['zonenschild.sql']
}

// Raumeinheit Gemeinde aufbereiten (bedingt zonentyp.sql und somit parzelle.sql)
task processGemeinden(type: SqlExecutor, dependsOn: processZonentypen) {
    database = [dbUriProcessing, dbUserProcessing, dbPwdProcessing]
    sqlFiles = ['gemeinde.sql']
}

// $td Gut: Gruppierung der zusammengehörenden SQL in Ordner export_to_pub
// Ergebnis tabellenweise in Zielschema kopieren (persistieren)
task copyDataFromProcessing(type: Db2Db, dependsOn: [processZonenschilde, processGemeinden]){
    sourceDb = [dbUriProcessing, dbUserProcessing, dbPwdProcessing]
    targetDb = [dbUriPub, dbUserPub, dbPwdPub]
    transferSets = [
        new TransferSet('export_to_pub/export_parzelle.sql', 'arp_sein_strukturdaten_pub_v1.parzelle', true),
        new TransferSet('export_to_pub/export_zonentyp.sql', 'arp_sein_strukturdaten_pub_v1.zonentyp', true),
        new TransferSet('export_to_pub/export_zonenschild.sql', 'arp_sein_strukturdaten_pub_v1.zonenschild', true),
        new TransferSet('export_to_pub/export_gemeinde.sql', 'arp_sein_strukturdaten_pub_v1.gemeinde', true)
    ];
}

// Ergebnis als XTF-Datei exportieren
task exportXtf(type: Ili2pgExport, dependsOn: copyDataFromProcessing){
    database = [dbUriPub, dbUserPub, dbPwdPub]
    dbschema = "arp_sein_strukturdaten_pub_v1"
    disableValidation = false
    dataFile = xtfPath
    models = "SO_ARP_SEin_Strukturdaten_Publikation_20250407"
}

// $td .../test/ ist das SEIN Testsystem? -> Pfadvariable Umgebungsabhängig setzen
// XTF-Datei auf den SFTP-Server hochladen
tasks.register('upload') {
    dependsOn 'exportXtf'
    doLast {
        ssh.run {
            session(remotes.sftpServer) {
                put from: xtfPath, into: "/opt/daten/gis_arp_raumdaten/test/"
            }
        }
        println "File uploaded to FTP server"
    }
}